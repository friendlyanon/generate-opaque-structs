find_package(CheckTypeAlign REQUIRED)
include(CheckTypeSize)

function(generate_opaque_structs)
  cmake_parse_arguments(PARSE_ARGV 0 "" "" "INPUT;TEMPLATE;OUTPUT" STRUCTS)
  if(NOT IS_ABSOLUTE "${_INPUT}")
    set(_INPUT "${CMAKE_CURRENT_SOURCE_DIR}/${_INPUT}")
  endif()
  get_filename_component(include_dir "${_INPUT}" DIRECTORY)
  get_filename_component(header "${_INPUT}" NAME)
  list(APPEND CMAKE_REQUIRED_INCLUDES "${include_dir}")
  list(APPEND CMAKE_EXTRA_INCLUDE_FILES "${header}")

  set(defines "")
  set(structs "")
  set(empty_line "")
  foreach(s IN LISTS _STRUCTS)
    string(TOUPPER "${s}" us)
    check_type_align("struct ${s}_impl" "${us}_ALIGN" BUILTIN_TYPES_ONLY)
    check_type_size("struct ${s}_impl" "${us}_SIZE" BUILTIN_TYPES_ONLY)
    string(APPEND defines "${empty_line}${${us}_ALIGN_CODE}\n${${us}_SIZE_CODE}")
    string(APPEND structs "${empty_line}struct ${s} {\n  _Alignas(${us}_ALIGN) unsigned char storage[${us}_SIZE];\n};")
    set(empty_line "\n\n")
  endforeach()

  configure_file("${_TEMPLATE}" "${_OUTPUT}" @ONLY)
endfunction()
